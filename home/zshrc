## vim: ft=zsh fdm=marker
## ~/.zshrc: zsh configuration file
## common ENV variables should go in ~/.profile or ~/.xprofile
## keep this for zsh specific code that can't go in ~/.zprofile

## Source ~/.profile
if [ -r "$HOME"/.profile ]; then
    emulate sh
    source "$HOME"/.profile
    emulate zsh
fi

typeset -U PATH
setopt appendhistory extendedglob hist_find_no_dups autocd hist_ignore_dups
unsetopt beep
export SAVEHIST=1000
# Emulate ls colors
export LS_COLORS='rs=0:di=01;34:ln=01;36:mh=00:pi=40;33:so=01;35:do=01;35:bd=40;33;01:cd=40;33;01:or=40;31;01:mi=00:su=37;41:sg=30;43:ca=30;41:tw=30;42:ow=34;42:st=37;44:ex=01;32:*.tar=01;31:*.tgz=01;31:*.arc=01;31:*.arj=01;31:*.taz=01;31:*.lha=01;31:*.lz4=01;31:*.lzh=01;31:*.lzma=01;31:*.tlz=01;31:*.txz=01;31:*.tzo=01;31:*.t7z=01;31:*.zip=01;31:*.z=01;31:*.Z=01;31:*.dz=01;31:*.gz=01;31:*.lrz=01;31:*.lz=01;31:*.lzo=01;31:*.xz=01;31:*.zst=01;31:*.tzst=01;31:*.bz2=01;31:*.bz=01;31:*.tbz=01;31:*.tbz2=01;31:*.tz=01;31:*.deb=01;31:*.rpm=01;31:*.jar=01;31:*.war=01;31:*.ear=01;31:*.sar=01;31:*.rar=01;31:*.alz=01;31:*.ace=01;31:*.zoo=01;31:*.cpio=01;31:*.7z=01;31:*.rz=01;31:*.cab=01;31:*.wim=01;31:*.swm=01;31:*.dwm=01;31:*.esd=01;31:*.jpg=01;35:*.jpeg=01;35:*.mjpg=01;35:*.mjpeg=01;35:*.gif=01;35:*.bmp=01;35:*.pbm=01;35:*.pgm=01;35:*.ppm=01;35:*.tga=01;35:*.xbm=01;35:*.xpm=01;35:*.tif=01;35:*.tiff=01;35:*.png=01;35:*.svg=01;35:*.svgz=01;35:*.mng=01;35:*.pcx=01;35:*.mov=01;35:*.mpg=01;35:*.mpeg=01;35:*.m2v=01;35:*.mkv=01;35:*.webm=01;35:*.ogm=01;35:*.mp4=01;35:*.m4v=01;35:*.mp4v=01;35:*.vob=01;35:*.qt=01;35:*.nuv=01;35:*.wmv=01;35:*.asf=01;35:*.rm=01;35:*.rmvb=01;35:*.flc=01;35:*.avi=01;35:*.fli=01;35:*.flv=01;35:*.gl=01;35:*.dl=01;35:*.xcf=01;35:*.xwd=01;35:*.yuv=01;35:*.cgm=01;35:*.emf=01;35:*.ogv=01;35:*.ogx=01;35:*.aac=00;36:*.au=00;36:*.flac=00;36:*.m4a=00;36:*.mid=00;36:*.midi=00;36:*.mka=00;36:*.mp3=00;36:*.mpc=00;36:*.ogg=00;36:*.ra=00;36:*.wav=00;36:*.oga=00;36:*.opus=00;36:*.spx=00;36:*.xspf=00;36:';

# {{{ Completion
# The following lines were added by compinstall
zstyle ':completion:*' completer _expand _complete _ignored _correct _approximate _prefix
zstyle ':completion:*' completions 1
zstyle ':completion:*' expand prefix suffix
zstyle ':completion:*' file-sort name
zstyle ':completion:*' glob 1
zstyle ':completion:*' group-name ''
zstyle ':completion:*' insert-unambiguous true
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*' list-prompt '%SAt %p: Hit TAB for more, or the character to insert%s'
zstyle ':completion:*' matcher-list '' 'm:{[:lower:]}={[:upper:]} r:|[._-]=* r:|=*'
zstyle ':completion:*' max-errors 1
zstyle ':completion:*' menu select=5
zstyle ':completion:*' original true
zstyle ':completion:*' select-prompt '%SScrolling active: current selection at %p%s'
zstyle ':completion:*' squeeze-slashes true
zstyle ':completion:*' substitute 1
zstyle :compinstall filename '/home/user/.shellrc/zsh.completion'

autoload -Uz compinit
compinit
# End of lines added by compinstall

zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path "$XDG_CACHE_HOME"/zsh/completion_cache
# }}}

# {{{ Prompt
# https://wiki.archlinux.org/index.php/zsh#Customizing_the_prompt
autoload -U promptinit && promptinit
autoload -U colors && colors

# {{{ Color defs
black="%{$fg[black]%}"
green="%{$fg[green]%}"
blue="%{$fg[blue]%}"
cyan="%{$fg[cyan]%}"
red="%{$fg[red]%}"
yellow="%{$fg[yellow]%}"
magneta="%{$fg[magneta]%}"
white="%{$fg[white]%}"
reset="%{$reset_color%}"
# }}}

# {{{ Vim mode indicator
# vim_ins_mode="%{$fg[yellow]%}[INS]%{$reset_color%}"
vim_ins_mode="%{$reset_color%}"
vim_cmd_mode="%{$fg[red]%}[CMD]%{$reset_color%}"
vim_mode=$vim_ins_mode

function zle-keymap-select {
  vim_mode="${${KEYMAP/vicmd/${vim_cmd_mode}}/(main|viins)/${vim_ins_mode}}"
  zle reset-prompt
}
zle -N zle-keymap-select

function zle-line-finish {
  vim_mode=$vim_ins_mode
}
zle -N zle-line-finish

# Fix a bug when you C-c in CMD mode and you'd be prompted with CMD mode indicator, while in fact you would be in INS mode
# Fixed by catching SIGINT (C-c), set vim_mode to INS and then repropagate the SIGINT, so if anything else depends on it, we will not break it
# Thanks Ron! (see comments)
function TRAPINT() {
  vim_mode=$vim_ins_mode
  return $(( 128 + $1 ))
}
# }}}

# {{{ Git prompt
setopt prompt_subst
autoload -Uz vcs_info
zstyle ':vcs_info:*' actionformats '%F{5}(%f%s%F{5})%F{3}-%F{5}[%F{2}%b%F{3}|%F{1}%a%F{5}]%f '
zstyle ':vcs_info:*' formats '%F{5}(%f%s%F{5})%F{3}-%F{5}[%F{2}%b%F{5}]%f '
zstyle ':vcs_info:(sv[nk]|bzr):*' branchformat '%b%F{1}:%F{3}%r'
zstyle ':vcs_info:*' enable git cvs svn

# or use pre_cmd, see man zshcontrib
vcs_info_wrapper() {
  vcs_info
  if [ -n "$vcs_info_msg_0_" ]; then
    echo "%{$fg[grey]%}${vcs_info_msg_0_}%{$reset_color%}$del"
  fi
}
# }}}

# {{{{
# Ranger Mode
function ranger_mode() {
    if [ -n "$RANGER_LEVEL" ]; then
        echo "$green% RANGER$reset"
    fi
}
# }}}}

PROMPT=$'${vim_mode}$yellow(%n@%m) $(ranger_mode)$reset:$green%3~$red%#$reset '
RPROMPT=$'$(vcs_info_wrapper)'
# }}}

# {{{ Keybinds
bindkey -v
KEYTIMEOUT=1 # instant mode switch

# Better history search with C-r and C-s
bindkey "^R" history-incremental-pattern-search-backward
bindkey "^S" history-incremental-pattern-search-forward

# Use the vi navigation keys in menu completion
zmodload zsh/complist
bindkey -M menuselect 'h' vi-backward-char
bindkey -M menuselect 'k' vi-up-line-or-history
bindkey -M menuselect 'l' vi-forward-char
bindkey -M menuselect 'j' vi-down-line-or-history

# Fix backspace not working in vi mode after using a or A
# see https://superuser.com/a/533685
bindkey "^?" backward-delete-char
bindkey "^W" backward-kill-word 
bindkey "^H" backward-delete-char      # Control-h also deletes the previous char
bindkey "^U" backward-kill-line

# commands, use bindkey -s 'key' 'command\n'
# you can find the by pressing it in cat, for reference control=^, alt=^[
# you can also use Control-v key in vim
bindkey -s 'e' 'ranger\n'
bindkey -s "t" 'tmux new-session -c "$HOME" -A -t "$USER"\n'
bindkey -s "m" 'ncmpcpp\n'

# {{{ ZKBD
autoload zkbd
ZKBDDIR=${ZDOTDIR:-$HOME}/.zkbd
ZKBDFILE=$ZKBDDIR/$TERM
if [ -r $ZKBDFILE ]; then
	source $ZKBDFILE
	[ -n ${key[Backspace]}	] && bindkey "${key[Backspace]}"	backward-delete-char
	[ -n ${key[Delete]}		] && bindkey "${key[Delete]}"		delete-char
	[ -n ${key[Insert]}		] && bindkey "${key[Insert]}"		overwrite-mode
	[ -n ${key[Home]}		] && bindkey "${key[Home]}"			beginning-of-line
	[ -n ${key[End]}		] && bindkey "${key[End]}"			end-of-line
	[ -n ${key[Left]}		] && bindkey "${key[Left]}"			backward-char
	[ -n ${key[Right]}		] && bindkey "${key[Right]}"		forward-char
	
	# History search based on current input
	[ -n "${key[Up]}"		] && bindkey "${key[Up]}"		history-beginning-search-backward
	[ -n "${key[Down]}"		] && bindkey "${key[Down]}"		history-beginning-search-forward
	[ -n "${key[PageUp]}"	] && bindkey "${key[PageUp]}"	history-search-backward
	[ -n "${key[PageDown]}"	] && bindkey "${key[PageDown]}"	history-search-forward
else
	# echo "No zkbd settings found for this terminal, falling back to terminfo."

    # Falllback to terminfo http://zshwiki.org/home/zle/bindkeys
    # create a zkbd compatible hash;
    # to add other keys to this hash, see: man 5 terminfo
    typeset -A key

    key[Home]="$terminfo[khome]"
    key[End]="$terminfo[kend]"
    key[Insert]="$terminfo[kich1]"
    key[Backspace]="$terminfo[kbs]"
    key[Delete]="$terminfo[kdch1]"
    key[Up]="$terminfo[kcuu1]"
    key[Down]="$terminfo[kcud1]"
    key[Left]="$terminfo[kcub1]"
    key[Right]="$terminfo[kcuf1]"
    key[PageUp]="$terminfo[kpp]"
    key[PageDown]="$terminfo[knp]"

    # setup key accordingly
    [ -n "$key[Backspace]"  ] && bindkey "$key[Backspace]" backward-delete-char
    [ -n "$key[Delete]"     ] && bindkey "$key[Delete]"    delete-char
    [ -n "$key[Insert]"     ] && bindkey "$key[Insert]"    overwrite-mode
    [ -n "$key[Home]"       ] && bindkey "$key[Home]"      beginning-of-line
    [ -n "$key[End]"        ] && bindkey "$key[End]"       end-of-line
	[ -n "$key[Left]"		] && bindkey "$key[Left]"      backward-char
	[ -n "$key[Right]"		] && bindkey "$key[Right]"     forward-char

	[ -n "$key[Up]"		    ] && bindkey "$key[Up]"		    history-beginning-search-backward
	[ -n "$key[Down]"		] && bindkey "$key[Down]"		history-beginning-search-forward
	[ -n "$key[PageUp]"	    ] && bindkey "$key[PageUp]"	    history-search-backward
	[ -n "$key[PageDown]"	] && bindkey "$key[PageDown]"	history-search-forward

    # Finally, make sure the terminal is in application mode, when zle is
    # active. Only then are the values from $terminfo valid.
    if (( ${+terminfo[smkx]}  )) && (( ${+terminfo[rmkx]}  )); then
        function zle-line-init () {
            echoti smkx
        }
        function zle-line-finish () {
            echoti rmkx
        }
        zle -N zle-line-init
        zle -N zle-line-finish
    fi
fi
# }}}
# }}}

# {{{ CD History
# cd -<NUMBER> to access recent directory list
# This will remember the dirstack across sessions
DIRSTACKSIZE=20
setopt autopushd pushdsilent pushdtohome
setopt pushdignoredups # remove duplicates
setopt pushdminus # reverts +/- operators
DIRSTACKFILE="$XDG_CACHE_HOME/zsh/dirs"
[[ -f $DIRSTACKFILE ]] || mkdir -p `dirname $DIRSTACKFILE` && touch $DIRSTACKFILE
if [[ -f $DIRSTACKFILE ]] && [[ $#dirstack -eq 0 ]]; then
  dirstack=( ${(f)"$(< $DIRSTACKFILE)"} )
  # Uncomment to remember and change to the last directory across sessions
  # [[ -d $dirstack[1] ]] && cd $dirstack[1]
fi
chpwd() {
  print -l $PWD ${(u)dirstack} >$DIRSTACKFILE
}
# }}}
